'USEUNIT EnoviaUtils
'USEUNIT ExcelLib
'USEUNIT StringLib
'USEUNIT WindowsUtility
'USEUNIT GlobalVars
'USEUNIT OR_Enovia
'USEUNIT EnoviaPDT_PDTUtils
'USEUNIT EnoviaPDT_ValidateParts
'USEUNIT EnoviaPDT_CreatePartFillFields
'USEUNIT EnoviaPDT_TabValidation
'USEUNIT WebObjectLib
'USEUNIT WebKeywords
'USEUNIT EnoviaUtils
'USEUNIT LogLib
'USEUNIT EnoviaPDT_CreateEditAttributesTables
'USEUNIT EnoviaPDT_GeneratePDFViewTemplate
sub test()
g_strBrowserType = "iexplore"
  EnoviaPDT_OpenTables()
End Sub 

Sub Main(sEnvironment, sUserId, bDevFlag)

  Dim PartType, PartTypeCount, RunValidations, GcasCode, Driver, aAlmIDs, sCreatePDFTemplate
  
  Call CreateEnvironment("Enovia PDT Regression", sEnvironment, bDevFlag, "Enovia PDT Regression", false, "Regression")
 
  ' Launch browser and navigate to environment URl
  Enovia_LaunchEnovia(sEnvironment)
  
  ' Login using identifier's creds
  blnResult = Enovia_Login(sUserId)
  
  'Login failed, terminate test
  If Not blnResult Then
    Log.Error("Cannot login into Enovia!")
    Runner.Stop
  End If
  
  ' Role selection
  Enovia_clickOK()
  
  ' Wait for home button
  Set objHomeButton = objGetObject(OR_EnoviaTB_HomeBtn,"",120000)
  Set objHomeButton = Nothing
  
  ' Loop througt the part types
  Driver = Excel_ReadExcelSheet(g_pdt_FileLocation&"Driver.xls", "Execute")
  
  For i = 0 to UBound(Driver)
    PartType = Driver(i,0)
    PartTypeCount = Driver(i,1)
    RunValidations = Driver(i,2)
    aAlmIDs = Driver(i,3)
    sCreatePDFTemplate = Driver(i,4)
    
    If PartTypeCount > 0 Then
      'Init
      g_isBlocked = False
      
      ' load categories and tabs into global var
      g_pdt_aCategoriesTabs = EnoviaPDT_GetTabsAndLinks(PartType)

      ' split and set alm IDS into array
      EnoviaPDT_SetAlmIDs(aAlmIDs)
    
      ' These parts are created when Formulation is created.
      If PartType = "Formulation Part" Or PartType = "Formulation Process" Then
      
        EnoviaPDT_OpenFormulationPart(PartType)
        Call LogTestCase(PartType&" - Create", EnoviaPDT_GetTestCaseID(), EnoviaPDT_GetAlmID(1))
        
        ' Need to click the category button get categories visible for formulation
        If PartType = "Formulation Part" Then
          Call Click(OR_EnoviaPDT_CategoriesBtn,"","")
        End If
        
        EnoviaPDT_FillTabs(PartType)
      Else
        ' Lets create as many parts that is set in Driver spreadsheet
        Log.AppendFolder("Create part: "&PartType)
        ' Test Cases 1-2
        blnResult = EnoviaPDT_CreatePartAndFillFields(PartType,PartTypeCount)
        Log.PopLogFolder
        
        If blnResult = False then
          Log.Warning("Unable to create part: "&PartType)
        End If
      End If
      
      ' Run Validations for all the tabs if part creation was successful.
      If RunValidations = "Y" Then
        Log.AppendFolder("Validate part: "&PartType)
        ' Test Case 3, Validate tab data
        Call EnoviaPDT_ValidatePartTabsData(PartType)
        
        ' Test case 4. No create page for these two part types
        If PartType <> "Formulation Part" And PartType <> "Formulation Process" Then
          Call EnoviaPDT_ValidateCreatePage(PartType)
          Delay 2000
        End If
        
        Call EnoviaPDT_ValidateAllTabs(PartType, EnoviaPDT_GetGcasByPartType(PartType))
        Log.PopLogFolder
      End If 
    
      ' Create spreadsheet for PDFViews
      If sCreatePDFTemplate = "Y" Then
        If EnoviaPDT_GeneratePDFTEmplateExcel(PartType,EnoviaPDT_GetGcasByPartType(PartType)) Then
          Log.Message("PDFView Template generated without errors")
        Else
          Log.Warning("Errors while generationg PDFView template. "&Err.Description)
        End If
      End If
      
      ' Initialize screen for next round
      Call Click(OR_EnoviaTB_HomeBtn2,"","")
      WaitPageLoad("")
    End if
    
  Next
  
  EnoviaPDT_PrintAllCreatedParts()
  
  'done executing, logging off.
  Enovia_Logout()
  CloseAllWindows()
  Call CloseEnvironment()
End Sub




